plugins {
    id 'com.cfnstacks.gradle.artifacts3' version '0.1.6'
    id 'org.ajoberstar.grgit' version '2.0.0'
}

group = 'com.cfnstacks'
description = 'Artifact Storage on S3'

ext.params = [:]
if(project.file('local-config.groovy').exists()) { apply from: 'local-config.groovy' }
def setParam(name, defaultValue) { (hasProperty(name)) ? property(name) : (params[name]) ? params[name] :defaultValue }

ext.params.with {
    failure = setParam('failure', 'ROLLBACK') // DO_NOTHING | ROLLBACK | DELETE
    group = setParam('group', 'com.cfnstacks')
    stackParamSet = setParam('stackParamSet', 'deploy')
    profile = setParam('profile', '')
    region = setParam('region', 'us-east-2')
    repo = setParam('repo', 'cfn-stacks.com.s3.us-east-2.amazonaws.com')
    stack = setParam('stack', 'artifacts3-repo')
    template = setParam('template', 'artifacts3-repo.yaml')
    // Template specific params below
    DomainName = setParam('DomainName', '')
    HostedZoneId = setParam('HostedZoneId', '')
    AcmCertificateArn = setParam('AcmCertificateArn', '')
    CloudFrontDefaultTTL = setParam('CloudFrontDefaultTTL', 86400) // One day is the default
    OriginAccessIdentity = setParam('OriginAccessIdentity', '')
    OriginAccessUser = setParam('OriginAccessUser', '')
}

ext.stackParameters = [
    deploy: [
        DomainName: params.DomainName,
        HostedZoneId: params.HostedZoneId,
        AcmCertificateArn: params.AcmCertificateArn,
        CloudFrontDefaultTTL: params.CloudFrontDefaultTTL,
        OriginAccessIdentity: params.OriginAccessIdentity,
        OriginAccessUser: params.OriginAccessUser
    ]
]

artifacts3 {
    group = params.group
    profileName = params.profile
    repo = params.repo
}

aws {
    profileName = params.profile
    region = params.region
}

cloudFormation {
    templateFile = project.file("build/cloudformation/${params.template}")
    stackName = params.stack
    capabilityIam true
    onFailure params.failure
    conventionMapping.stackParams = { project.ext.stackParameters[project.ext.params.stackParamSet] }
}

asciidoctor {
    backends 'html5', 'pdf'
    attributes \
        'build-gradle': file('build.gradle'),
        'stylesheet': 'stylesheets/style.css',
        'endpoint-url': 'https://cfn-stacks.com',
        'source-highlighter': 'coderay',
        'imagesdir': 'images',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'idprefix': '',
        'idseparator': '-',
        'docinfo1': '',
        'projectVersion': project.version,
        'gitHash': grgit.head().id,
        'gitHashShort': grgit.head().abbreviatedId
}